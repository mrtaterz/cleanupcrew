//breakables
actor GlassWreckDebris : AmmoWreckDebris //defined in mapitem.dec
{
	scale 0.3
	alpha 0.5
	States
	{
		Spawn:
			DEB3 ABCDEFGH 2
			loop
	}
}

actor ShrapnelParticle : AmmoWreckDebris //defined in mapitem.dec
{
	scale 0.5
	alpha 0.5
	States
	{
		Spawn:
			DEB3 ABCDEFGH 2
			loop
	}
}

actor usColumn : Column replaces Column
{
	+shootable
	+noblood
	health 20
	deathHeight 48
	damagefactor "burns", 0.25
	deathSound "world/lampbreak"
	mass 10000000
	//$Arg0Default 0
	//$Arg1Default 0
	//$Arg0 "Sector tag"
	//$Arg1 "Darken sector by"
	states
	{
		spawn:
			COLU A 1 NoDelay A_PlaySound("world/bulbhum",CHAN_VOICE,1,true)
			goto super::spawn
		death:
			BCOL A 0 A_SpawnDebris("GlassWreckDebris")
			BCOL A 0 A_StopSound(CHAN_VOICE)
			BCOL A 0 A_Scream
			BCOL A 1 A_Jump(128,"dofx")
			BCOL A 0 A_PlaySound("world/bulbfiz1")
		dofx:
			BCOL A 0 A_JumpIf(args[0]==0,"xloop")
			BCol A 0 Light_LowerByValue(args[0],args[1])
		xloop:
			BCOL A 2 Light("LAMPFLASH")
			BCOL A -1
			stop
	}
}

actor usTechLamp : TechLamp replaces TechLamp
{
	+shootable
	+noblood
	health 20
	deathHeight 80
	damagefactor "burns", 0.25
	deathSound "world/lampbreak"
	mass 10000000
	//$Arg0Default 0
	//$Arg1Default 0
	//$Arg0 "Sector tag"
	//$Arg1 "Darken sector by"
	states
	{
		death:
			BTL1 A 0 A_SpawnDebris("GlassWreckDebris")
			BTL1 A 0 A_Scream
			BTL1 A 1 A_Jump(128,"dofx")
			BTL1 A 0 A_PlaySound("world/bulbfiz2")
		dofx:
			BTL1 A 0 A_JumpIf(args[0]==0,"xloop")
			BTL1 A 0 Light_LowerByValue(args[0],args[1])
		xloop:
			BTL1 A 2 Light("LAMPFLASH")
			BTL1 A -1
			stop
	}
}

actor usTechLamp2 : TechLamp2 replaces TechLamp2
{
	+shootable
	+noblood
	health 20
	deathHeight 60
	damagefactor "burns", 0.25
	deathSound "world/lampbreak"
	mass 10000000
	//$Arg0Default 0
	//$Arg1Default 0
	//$Arg0 "Sector tag"
	//$Arg1 "Darken sector by"
	states
	{
		death:
			BTL2 A 0 A_SpawnDebris("GlassWreckDebris")
			BTL2 A 0 A_Scream
			BTL2 A 1 A_Jump(128,"dofx")
			BTL2 A 0 A_PlaySound("world/bulbfiz2")
		dofx:
			BTL2 A 0 A_JumpIf(args[0]==0,"xloop")
			BTL2 A 0 Light_LowerByValue(args[0],args[1])
		xloop:
			BTL2 A 2 Light("LAMPFLASH")
			BTL2 A -1
			stop
	}
}

Actor GreyLamp 2100
{
  //$Category "Light Sources"
	+Solid
	Radius 8
	Height 52
	+shootable
	+noblood
	health 15
	deathHeight 52
	damagefactor "burns", 0.25
	deathSound "world/lampbreak"
	mass 10000000
	//$Arg0Default 0
	//$Arg1Default 0
	//$Arg0 "Sector tag"
	//$Arg1 "Darken sector by"
	States
	{
		Spawn:
			GLMP A -1 Bright
			Loop
		death:
			BGLM A 0 A_SpawnDebris("GlassWreckDebris")
			BGLM A 0 A_Scream
			BGLM A 1 A_Jump(128,"dofx")
			BGLM A 0 A_PlaySound("world/bulbfiz2")
		dofx:
			BGLM A 0 A_JumpIf(args[0]==0,"xloop")
			BGLM A 0 Light_LowerByValue(args[0],args[1])
		xloop:
			BGLM A 2 Light("LAMPFLASH")
			BGLM A -1
			stop
	}
}

actor ChairNoiseCooldown : Powerup
{
	Powerup.duration 24
}

//inkoalawetrust: vvvvvvvvvvv Average Doom modding experience vvvvvvvvvvvvvvvvvvvv
// Scripted's old chair was hot fucking cheeks, completely rubbish and didn't work
// i then resolved to fix it "as was" but it fucking nearly nuked my PC
// almost lost my latest TLT.pk3 (that I spent dozens of hours cooking michelin
// three star meals) thankfully slade does backups so I threw scripted's POS code in 
// the bin and edited a TLT (ZScripted originally!!!) shopping cart into this new
// not fucking garbage chair

actor Chair 106
{
	//$Category Decoration
    Radius 15
    Height 30
    Mass 15
    MaxStepHeight 4
	health 50
    deathsound "misc/chairdestroy"
	PushFactor 1
	+FLOORCLIP
    +PUSHABLE
    +NOBLOOD
    +SHOOTABLE
	+SOLID
    +CANPASS
	States
	{
		Spawn:
			CHAI A 1
			goto pushing
		pushing:
			CHAI A 8 A_LookEx(LOF_NOSOUNDCHECK, 0, 64, 0, 360)
			loop
		See:
			CHAI A 0 A_JumpIf(abs(velX) != 0 || abs(velY) != 0, "Moving")
			goto Spawn
		//spin right
		Moving:
			CHAI A 0 A_Jump(128, "Moving2")
			CHAI A 1 ACS_NamedExecuteWithResult("chairspe")
			CHAI A 0 A_SetArg(1,45)
		Speen:
			CHAI A 2 A_JumpIf(abs(velX) < 0.01 || abs(velY) < 0.01, "Spawn")
			CHAI A 4 A_SetAngle(angle + args[1])
			CHAI A 0 A_SetArg(1,args[1] + (args[1]/2))
			CHAI A 0 ACS_NamedExecuteWithResult("chairspe")
			loop
		//spin left
		Moving2:
			CHAI A 1 ACS_NamedExecuteWithResult("chairspe")
			CHAI A 0 A_SetArg(1,-45)
		Speen2:
			CHAI A 2 A_JumpIf(abs(velX) < 0.01 || abs(velY) < 0.01, "Spawn")
			CHAI A 4 A_SetAngle(angle + args[1])
			CHAI A 0 A_SetArg(1,args[1] + (args[1]/2))
			CHAI A 0 ACS_NamedExecuteWithResult("chairspe")
			loop
		Death:
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			TNT1 AAAAA 0 A_SpawnItemEx ("ShrapnelParticle", 0, 0, random (3, 5), random (-5, 5), random (-5, 5), random (1, 2), 0, 0, 30)
			Stop
	}
}

actor WolfChair1 2102
{
	//$Category Decoration
	//$Title "Metal Chair"
    Radius 9
    Height 28
    Mass 50
    MaxStepHeight 4
	health 50
    deathsound "misc/boxdestroy"
	PushFactor 0.5
	+FLOORCLIP
    +PUSHABLE
    +NOBLOOD
    +SHOOTABLE
	+SOLID
    +CANPASS
	States
	{
		Spawn:
			WCHR A 1
			goto pushing
		pushing:
			WCHR A 8 A_LookEx(LOF_NOSOUNDCHECK, 0, 64, 0, 360)
			loop
		See:
			WCHR A 0 A_JumpIf(abs(velX) != 0 || abs(velY) != 0, "Moving")
			goto Spawn
		Moving:
			WCHR A 25 A_PlaySound("misc/chairmove/2", CHAN_AUTO)
			goto Spawn
		Death:
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			TNT1 AAAAA 0 A_SpawnItemEx ("ShrapnelParticle", 0, 0, random (3, 5), random (-5, 5), random (-5, 5), random (1, 2), 0, 0, 30)
			Stop
	}
}

actor WolfTable4 2101
{
	//$Title "Metal Table"
	//$Category Decoration
	Radius 22
	Height 26
	health 80
	mass 999999
	deathsound "misc/boxdestroy"
	+SOLID
	+NOBLOOD
	+SHOOTABLE
	+CANPASS
	States
	{
		Spawn:
			WTBL D -1
			Stop
		Death:
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			TNT1 AAAAA 0 A_SpawnItemEx ("ShrapnelParticle", 0, 0, random (3, 5), random (-5, 5), random (-5, 5), random (1, 2), 0, 0, 30)
			Stop
	}
}

//rats
ACTOR Rat 9656
{
    Health 10
    Radius 6
    Height 6
    Scale 0.5
    Mass 200
    Speed 5
    PainChance 255
	SeeSound "Rat/Sight"
	PainSound "Rat/Pain"
	DeathSound "Rat/Death"
    Obituary "$OB_RAT"
    MONSTER
    +DONTHURTSPECIES
    +NOINFIGHTING
    +FRIGHTENED
	-solid
    States
    {
    Spawn:
        RATA AAAABBBBCCCC 1 A_Wander
		TNT1 A 0 A_LookEx(0,0,32,512,360)
        TNT1 A 0 A_Jump(8,"Scanning")
        Loop
	Forget:
		TNT1 A 0 A_ClearTarget
    See:
	xloop:
        RATA AAAABBBBCCCC 1 A_Wander
        TNT1 A 0 A_JumpIfCloser(64,"tryChase")
        TNT1 A 0 A_Jump(16,"Scanning")
        Loop
	tryChase:
		TNT1 A 0 A_Jump(16,"goChase")
		goto See
    Scanning:
		TNT1 A 0 A_ClearTarget
        RATA AAAAAAAAAAAA 1 A_LookEx(0,0,32,512,360)
        TNT1 A 0 A_Jump(128,"xloop")
        Loop
	goChase:
		TNT1 A 0 A_PlaySound("Rat/Sight")
    Chase:
		RATA AAAABBBBCCCC 1 A_Chase
		TNT1 A 0 A_Jump(4,"Forget")
		Loop
    Melee:
		RATA A 20 A_FaceTarget
		TNT1 A 0 A_PlaySound("Rat/Attack")
		RATA B 3 A_CustomMeleeAttack(1,0,0,0,0)
		Goto See
    Pain:
        TNT1 A 0 A_Pain
        Goto Chase
	Death.Dark:
		TNT1 A 0 A_GiveToTarget("Health",5)
    Death:
        RATD A 1 A_Scream
        RATD AAABBBBCCCC 1
        Stop

    }
}

//Camera prop, for some reason it needs to be explicitly set as having a valid angle.
Actor UnseenCameraProp 9657
{
	//$Title CCTV Camera
	//$Category Decoration
	//$Angled
	
	+NOGRAVITY
	+SOLID
	Radius 12
	Height 24
	
	States
	{
		//Speen
		Spawn:
			MRC1 B 0 NoDelay A_CheckFlag ("StandStill","Lazy")
			xloop:
			//with the default setting of 34, the camera takes 595 (!) ticks to fully cycle
			//four times, with a slight discrepancy between the animation and the actual position of the camera
			//thus, i have to define the animation cycle four times, with slightly varying durations to ensure
			//proper synchronization of animation and camera position
			MRC1 BCDEFGHHGFEDCB 22
			MRC1 BCDEFGHHGFEDCB 22
			MRC1 BCDEFGH 20
			MRC1 H 6
			MRC1 HGFEDCB 20
			MRC1 B 8
			MRC1 BCDEFGH 20
			MRC1 HGFEDCB 20
			Loop
		Lazy:
			MRC1 A -1
			Loop
	}
}

//how idiotic do you think it is that missiles cannot hit shit if they can't move?
//so much for that use case. we're going a different, far more awful direction.
actor BushBase : CustomInventory
{
	states
	{
		pickup:
			TNT1 A 0 A_JumpIfInventory("PowerBushCooldown",1,"donothing")
			TNT1 A 0 A_GiveInventory("PowerBushCooldown",1)
			TNT1 A 0 A_SpawnItemEx("BushNoiseMaker")
			fail
		donothing:
			TNT1 A 0
			fail
	}
}

actor PowerBushCooldown : Powerup
{
	Powerup.duration 25
}

actor BushNoiseMaker : DistantExploMaker
{
	states
	{
		spawn:
			TNT1 A 35 NODELAY A_PlaySound("world/bush")
			stop
	}
}

actor Bush : BushBase 9406
{
	//$Title Random Bush
	//$Category Decoration
	//$Sprite BUS2A0
	Radius 24
	Height 32
    States
	{
		Spawn:
			TNT1 A 0 NoDelay A_Jump(128, "Spawn2")
			BUS2 A 1
			goto xloop
		Spawn2:
			BUS2 B 1
		xloop:
			"####" "#" -1
			stop
	}
}

actor VendingMachine 9658
{
	//$Category Decoration
	//$Title Vending Machine
	tag "Vending Machine"
	Health 50
	Height 72
	DeathHeight 78
	Radius 24
	Mass 1000000
	//Activation THINGSPEC_Switch
	deathsound "misc/boxdestroy"
	+SOLID
	+NOBLOOD
	+NOBLOODDECALS
	+NOTAUTOAIMED
	+SHOOTABLE
	+DONTSPLASH
	+CANPASS
	states
	{
		Spawn:
			VEND A 1
			Loop
		Death:
			VEND B 1 A_Scream
			VEND B 0 A_SpawnItemEx("UnseenCanSpawner", 32)
			VEND B -1
			stop
	}
}

actor UnseenCanSpawner
{
	+NOGRAVITY
	+NOINTERACTION
	+NOBLOCKMAP
	-SOLID
	+THRUACTORS
	+CANPASS
	States
	{
		Spawn:
			TNT1 A 2
			TNT1 A 0 A_SpawnItemEx("UnseenCanProp", 0, random(-4,4), 5, frandom(6,12), 0, 0, frandom(-35, 35))
			TNT1 A 2
			TNT1 A 0 A_SpawnItemEx("UnseenCanProp", 0, random(-4,4), 5, frandom(6,12), 0, 0, frandom(-35, 35))
			TNT1 A 2
			TNT1 A 0 A_SpawnItemEx("UnseenCanProp", 0, random(-4,4), 5, frandom(6,12), 0, 0, frandom(-35, 35))
			TNT1 A 2
			TNT1 A 0 A_SpawnItemEx("UnseenCanProp", 0, random(-4,4), 5, frandom(6,12), 0, 0, frandom(-35, 35))
			TNT1 A 2
			TNT1 A 0 A_SpawnItemEx("UnseenCanProp", 0, random(-4,4), 5, frandom(6,12), 0, 0, frandom(-35, 35))
			TNT1 A 2
			TNT1 A -1
			Stop
	}
}

//TODO: add the tumble animation
actor UnseenCanProp : SwitchableDecoration 9659
{
	//$Title Soda Can
	//$Category Health and Armor
	Radius 5
	Height 5
	Scale 0.5
	Speed 6
	Gravity 1.0
	Mass 5
	Activation THINGSPEC_ThingTargets|THINGSPEC_Switch
	+BUMPSPECIAL
	+SOLID
	-NOGRAVITY
	+PUSHABLE
	+CANPASS
	//+THRUACTORS
	Pushfactor 1.0
	States
	{
		Spawn:
			CANC A 2
			Loop
		Inactive:
		Active:
			TNT1 A 0 A_GiveToTarget("UnseenCanDummy", 1)
			TNT1 A 0 A_PlaySound("items/can", CHAN_AUTO)
			TNT1 A 0 A_NoBlocking
			TNT1 A -1
			Stop
	}
}

actor UnseenCanDummy : Health
{
	+INVENTORY.ALWAYSPICKUP
	Inventory.Amount 1
	Inventory.MaxAmount 100
	Inventory.PickupMessage "Picked up a can of soda"
	States
	{
	Spawn:
		CANA A 1
		Loop
	}
}

