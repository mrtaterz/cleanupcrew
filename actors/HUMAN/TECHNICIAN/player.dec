//the human sapper class and its corpses

//UAC Detachment 4F Technician--
//Detachment 4F's sappers are drawn from the various combat engineer units under
//the UAC. Although typically trained in explosives handling, Detachment 4F sees
//to it that they recieve training in all kinds of heavy weaponry in the event
//they might need to use it.
//For this task, alongside their typical mine laying device they are also issued
//a flame-thrower to sweep large areas with to clear them out.
//And anything that runs may just trip- perhaps over a UAC M67 fragmentation
//mine.

//"Okay, what about temperature? What happens if we change it?"

Actor Sapper : PlayerPawn
{
	limitedtoteam 0
	//speed 0.9
	Player.StartItem "Flamer"
	Player.StartItem "usFist"
	Player.StartItem "MineLauncher"
	Player.StartItem "usClip", 50
	Player.StartItem "usCell", 100
	Player.StartItem "Mines", 5
	Player.StartItem "GreenArmor"
	Player.StartItem "FlareStick", 5
	Player.StartItem "usPistol"
	Player.StartItem "PistolAmmo", 50
	Player.StartItem "PistolMagazine", 10
	Player.WeaponSlot 1, usFist
	Player.WeaponSlot 2, usPistol
	Player.WeaponSlot 3, Flamer
	Player.WeaponSlot 4, MineLauncher
	Player.SoundClass "Sapper"
	player.DisplayName "Technician"
	Player.ColorRange 112, 127
	Player.scoreIcon "TECHI"
	Player.Face "STE"
	Player.RunHealth 20
	Player.SideMove 0.8
	Player.GruntSpeed 8
	Player.CrouchSprite "PFLC"
	RenderStyle Normal
	StencilColor green
	+USEDAMAGEEVENTSCRIPT
	States
	{
	Spawn:
		PFLM A -1
		Loop
	See:
		PFLM ABCD 4 
		Loop
	Missile:
		PFLM E 12
		Goto Spawn
	Melee:
		PFLM F 6 BRIGHT Light("ZOMBIEATK")
		Goto Missile
	Pain:
		PFLM G 4 A_Quake(1,10,0,1,"none")
		PFLM G 4 A_Pain
		Goto Spawn
	Pain.Falling:
		TNT1 A 0 A_SetSpeed(0.5)
		PFLM G 8 A_Quake(2,64,0,1,"none")
		PFLM G 4 A_PlayerScream
		PFLM G 2 A_SetSpeed(1.0)
		Goto Spawn
	death.darken:
		PFLM H 0 A_CustomMissile("BloodSpiller")
	death.suicide:
	Death:
		PFLM H 8 A_PlaySound("misc/flatline",16,1)
		PFLM I 8 A_PlayerScream
		PFLM J 8 A_NoBlocking
		PFLM KL 8 //M 8
		PFLM N 1 A_SpawnItemEx("DeadSapper",0,0,0,0,0,0,0,SXF_TransferTranslation)
		TNT1 A -1
		Stop
	XDeath:
		PFLM OP 5 A_Quake(2,32,0,1,"none")
		PFLM QR 5 A_XScream
		PFLM ST 5 A_NoBlocking
		PFLM UV 5
		PFLM W -1 //A_SpawnItemEx("GibbedSapper",0,0,0,0,0,0,0,SXF_TransferTranslation)
		TNT1 A -1
		Stop
	}
}

Actor DeadSapper : CorpseGeneric
{
	states
	{
		spawn:
			PFLM N 1 NoDelay A_GiveInventory("Clip",5)
			goto bedead
	}
}

Actor GibbedSapper : CorpseGeneric
{
	states
	{
		spawn:
			PFLM W 1 NoDelay A_GiveInventory("Clip",5)
			goto bedead
	}

}
















