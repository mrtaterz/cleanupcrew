//equipment base
//sets up required shit for making a pickupable equipment

actor Equipment
{
	+USESPECIAL
	+VULNERABLE
	+noBlood
	Activation THINGSPEC_ThingActs | THINGSPEC_ThingTargets
	Health 10
	DamageFactor "None",0
	DamageFactor "Fire",0
	DamageFactor "Poison",0
	DamageFactor "Grenade",1
	States
	{
		Death.Grenade:
			TNT1 A 1
			Stop
	}
}

//inventory items

//flare--
//A standard UAC-issued signal flare. Useful for signalling supply drops,
//vehicle breakdowns, and, yes, providing light in the dark.
actor FlareStick : CustomInventory
{
	tag "$TAG_FLARE"
	+Inventory.invbar
	Inventory.maxAmount 10
	Inventory.amount 1
	Inventory.icon "FLARE0" //happy accident
	States
	{
		Use:
			TNT1 A 0 A_FireCustomMissile("LitFlareStick",0,false)
			Stop
	}
}

//flare projectile thing
actor LitFlareStick
{
	Speed 16
	Radius 8
	Height 4
	+noblockmap
	+noblockmonst
	reactionTime 1050 //30s
	States
	{
		Spawn:
			FLAR A 0 NoDelay A_PlaySound("items/flare/light",CHAN_WEAPON)
			FLAR B 1 A_PlaySound("items/flare/burn",CHAN_BODY,0.5,true)
		xloop:
			FLAR A 1 A_CountDown
			FLAR B 1 A_CountDown
			loop
		Death:
			TNT1 A 1
			stop
	}
}

//ammos

/*
7.62x51mm UAC-S Machinegun magazine--
Ammunition for the Assault Troop's standard Machinegun.
This particular high-velocity load, while not as powerful as other
contemporary loads, is very lightweight and easy to control*, and allows the
Machinegun to fire with an extroadinarily high cyclic rate while still
offering satisfactory terminal performance.
*/
// use /**/ instead of // for long commented out code!

// Easy to control for a 7.62x51mm.
actor usClip : Ammo
{
	Inventory.Amount 10
	Inventory.MaxAmount 250
	Ammo.BackpackAmount 10
	Ammo.BackpackMaxAmount 500
	Inventory.Icon "CLIPA0"
	Tag "$TAG_CLIP"
	States
	{
	Spawn:
		CLIP A -1
		Stop
  }
}

//9mm Ball pistol ammo
actor PistolAmmo : Ammo
{
	Inventory.Amount 10
	Inventory.MaxAmount 50
	Ammo.BackpackAmount 10
	Ammo.BackpackMaxAmount 100
	Inventory.Icon "CLIPA0"
	Decal "BulletChip" //say wha
	States
	{
	Spawn:
		CLIP A -1
		Stop
  }
}

//12-gauge 00 buckshot--
//It's 100 years into the future and it's still the most common type of
//ammunition in the world. Is there anything else to say?
actor usShell : Ammo
{
	Inventory.Amount 4
	Inventory.MaxAmount 100
	Ammo.BackpackAmount 4
	Ammo.BackpackMaxAmount 200
	Inventory.Icon "SHELA0"
	Tag "$TAG_SHELL"
	States
	{
	Spawn:
		SHEL A -1
		Stop
  }
}

//40x46mm High-Explosive Dual-Purpose Grenades--
//These highly explosive cans of copper-lined Comp B were originally designed
//with usage against lightly armored vehicles in mind. Given the nature of the
//Threat, and the load's fragmentation also proving sufficient for
//anti-personnel use, this particular ammunition type was deemed warranted.
actor usRocketAmmo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 50
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 100
	Inventory.Icon "ROCKA0"
	Tag "$TAG_ROCKETAMMO"
	States
	{
	Spawn:
		ROCK A -1
		Stop
  }
}

//Napalm--
//Also known as styro-pyro.
//Used as fuel for the flame-thrower.
actor usCell : Ammo
{
	Inventory.Amount 50
	Inventory.MaxAmount 400
	Ammo.BackpackAmount 50
	Ammo.BackpackMaxAmount 800
	Inventory.Icon "CELLA0"
	Tag "$TAG_CELL"
	States
	{
	Spawn:
		CELL A -1
		Stop
  }
}

//5.56mm rifle magazine--
//Ammunition for the Assault Troop's Semi-Automatic rifle. Packs a punch
//with minimal recoil. Currently unused.
actor RifleClip : Ammo
{
	Inventory.Amount 50
	Inventory.MaxAmount 250
	Ammo.BackpackAmount 59
	Ammo.BackpackMaxAmount 500
	Inventory.Icon "CLIPA0"
	States
	{
	Spawn:
		CLIP A -1
		Stop
  }
}

//Grenades (unused) (ish!)
actor GrenadeAmmo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 50
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 100
	Inventory.Icon "SGRNA8A2"
	States
	{
	Spawn:
		SGRN A -1
		Stop
  }
}

//Proximity mines
actor Mines : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 10
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 20
	Inventory.Icon "PROXD0"
	tag "$TAG_MINEAMMO"
	States
	{
	Spawn:
		PROX D -1
		Stop
  }
}

//Proximity sensors
actor Sensors : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 5
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 10
	Inventory.Icon "SENSD0"
	tag "$TAG_SENSORAMMO"
	States
	{
		Spawn:
			SENS C -1
			stop
	}
}

//guns

//brass knuckles--
//A melee weapon preferred by many marines under the UAC as a last resort due to
//its ease of use, and Detachment 4F is no different in their preference.
//Unlike most troops under the UAC, Detachment 4F are also proficient in kicks.
actor usFist : Fist
{
	Inventory.icon "FISPA0"
	AttackSound "*fist"
	+Weapon.noAlert
	states
	{
		select:
			PUNG A 0 A_Raise
			PUNG A 1 A_Raise
			loop
		deselect:
			PUNG A 0 A_Lower
			PUNG A 1 A_Lower
			loop
		Fire:
			PUNG B 4
			PUNG C 4 A_CustomPunch(4*random(1,10),1,0,"usPunchPuff")
			PUNG D 5
			PUNG C 4
			PUNG B 5 A_ReFire
			Goto Ready
		AltFire:
			KICK A 4 A_GiveInventory("PowerKicking",1)
			KICK B 5
			KICK C 5 A_CustomPunch(6*random(1,10),1,0,"KickPuff",86)
			KICK B 4
			KICK A 4
			KICK A 0 A_Refire
			Goto Ready
	}
}

actor usPunchPuff : BulletPuff //override puffplus
{}

actor KickPuff : BulletPuff //more knockback
{
	projectileKickback 500
	damageType "kick"
	+puffGetsOwner
	+puffOnActors
	states
	{
		Melee:
		Spawn:
			TNT1 A 0 NoDelay A_GiveToTarget("funcKickRecoil",1)
		Crash:
			goto super::melee
	}
}

actor PowerKicking : PowerSpeed //stop the player when kicking
{
	Powerup.duration 18
	speed 0.3
	+PowerSpeed.noTrail
}

actor funcKickRecoil : CustomInventory //knock back the player if they kick something
{
	+Inventory.alwaysPickup
	states
	{
		pickup:
			TNT1 A 0 A_Recoil(5)
			stop
	}
}

//boingy boi
actor CaseBase
{
	+clientsideonly
	bounceType doom
	bounceFactor 0.6
	damage 0
	+bounceonactors
	projectile
	-nogravity
	radius 2
	height 4
	states
	{
		spawn:
			BAL1 A 1
			loop
		death:
		bedead:
			"####" "#" 35 A_SetTics(CallACS("c_getcasinglife")*35)
		fade:
			"####" "#" 1 A_FadeOut(0.1)
			loop
	}
}

//boingy boi maker
//this is a trick i learned from hexeretic
actor CaseSpawnerBase
{
	projectile
	speed 6
	damage 0
	radius 2
	height 4
	+clientsideonly
	states
	{
	spawn:
		TNT1 A 1 NoDelay A_SpawnItemEx("CaseBase",0,0,0,0,-3,4)
		stop
	}
}

actor CasingSteamer
{
	radius 2
	height 4
	+clientsideonly
	+noblockmap
	+nosector
	states
	{
		spawn:
			TNT1 A 35 NoDelay A_SetTics(random(5,20))
			TNT1 A 0 A_Warp(AAPTR_TRACER)
			TNT1 A 0 A_CustomMissile("CaseSteam",0,0,random(0,360),CMF_aimDirection,random(0,-90))
			TNT1 A 35 A_SetTics(random(5,20))
			TNT1 A 0 A_Warp(AAPTR_TRACER)
			TNT1 A 0 A_CustomMissile("CaseSteam",0,0,random(0,360),CMF_aimDirection,random(0,-90))
			TNT1 A 35 A_SetTics(random(5,20))
			TNT1 A 0 A_Warp(AAPTR_TRACER)
			TNT1 A 1 A_CustomMissile("CaseSteam",0,0,random(0,360),CMF_aimDirection,random(0,-90))
			TNT1 A 35 A_SetTics(random(5,20))
			TNT1 A 0 A_Warp(AAPTR_TRACER)
			TNT1 A 0 A_CustomMissile("CaseSteam",0,0,random(0,360),CMF_aimDirection,random(0,-90))
			TNT1 A 35 A_SetTics(random(5,20))
			TNT1 A 0 A_Warp(AAPTR_TRACER)
			TNT1 A 0 A_CustomMissile("CaseSteam",0,0,random(0,360),CMF_aimDirection,random(0,-90))
			TNT1 A 35 A_SetTics(random(5,20))
			TNT1 A 0 A_Warp(AAPTR_TRACER)
			TNT1 A 1 A_CustomMissile("CaseSteam",0,0,random(0,360),CMF_aimDirection,random(0,-90))

			stop
	}
}

//hacky zandro inventory tripe
//i don't miss this shit, now the lore
//refer to pistol ammo for actual ammo, and not this dummy item

//10mm pistol magazine--
//Standard issue pistol ammunition. Replaced the 9mm in service after rigorous
//trials.
//Unused!
actor PistolMagazine : Ammo
{
	Inventory.Amount 0
	Inventory.MaxAmount 10
	Inventory.Icon "CLIPA0"
	States
	{
		Spawn:
			CLIP A -1
			Stop
	}
}

actor WeaponCurrentlyZoomed : Inventory
{
    Inventory.MaxAmount 1
    +INVENTORY.UNTOSSABLE
    -INVENTORY.INVBAR
	
	//just in case some dumbarse spawns inherited actors in and cry about it crashing the game
	States
    {
    	Spawn:
    		TNT1 A 0
    		Stop
    }
}

//10mm pistol--
//A useful sidearm based off of the M92 issued to all units under the UAC.
//Packs a considerable punch, at least as far as pistols go.
actor usPistol : Pistol
{
	//Weapon.AmmoType1 "PistolMagazine" //why tf was this called a clip it's not a fucking pre-schnellfuer mauser c96 IT USES A MAG!!!!
	//because we're making a BAD DOOM MOD THEMED DOOM MOD and that comes complete with improper terminology.
	//but oh well, you changed it and i'm not gonna change it back LOL
	Weapon.Kickback 25	
	Weapon.AmmoType "PistolAmmo"
	//+WEAPON.NOAUTOFIRE
	Decal "BulletChip"
	States
	{
		Ready:
			APIS A 1 A_WeaponReady(WRF_ALLOWZOOM)
			Loop
		Deselect:
			APIS A 0 A_Lower
			APIS A 1 A_Lower
			Loop
		Select:
			APIS A 0 A_Raise
			APIS A 1 A_Raise
			Loop
		Fire:
			APIS A 0 A_FireBullets(2.4,1.514,-1,6) //why does zandro not have spawnheight yet? absolute rubbish source port smh
			APIS A 0 A_PlaySound ("weapons/pistolx", CHAN_WEAPON)
			APIS A 0 A_PlaySound ("weapons/pistoldist", CHAN_5)
			APIS A 0 A_FireCustomMissile("9MilCaseSpawner",0,0)
			APIS B 1 Bright A_Light(4) //god i don't miss the ancient method of doing this, need this to be 8 frames length in total about 4.375 shots per second
			APIS C 1 A_Light(2)
			APIS DE 1
			APIS F 1 A_JumpIf(CallACS("checkpistolreload"),"Reload")
			APIS GH 1 
			Goto Ready
		Zoom:
			TNT1 A 0 A_JumpIfInventory("WeaponCurrentlyZoomed", 1, "ZoomOut")
			TNT1 A 5 A_ZoomFactor(5.0)
			TNT1 A 0 A_GiveInventory("WeaponCurrentlyZoomed", 1)
		ZoomLoop:
			TNT1 A 35 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOSWITCH|WRF_NOFIRE)
			Loop
		ZoomOut:
			TNT1 A 0 A_TakeInventory("WeaponCurrentlyZoomed", 1)
			TNT1 A 5 A_ZoomFactor(1.0)
			Goto Ready
		Reload:
			APIR A 0 A_JumpIfNoAmmo("Ready")
			APIR ABCD 2
			APIR E 2 A_PlaySound("weapons/pistol/magout", CHAN_7)
			APIR FGH 2
			APIR I 2 A_PlaySound("weapons/pistol/magdrop",CHAN_6)
			APIR JKLM 2
			APIR N 2 A_PlaySound("weapons/pistol/magslide",CHAN_7)
			APIR OPQ 2
			APIR R 2 A_PlaySound("weapons/pistol/magin",CHAN_7)
			APIR ST 2
			APIR U 2 A_PlaySound("weapons/pistol/slide",CHAN_7)
			APIR VWXYZ 2
			APIS IJ 2
			goto Ready
		Spawn:
			PIST A -1
			Stop
	}
}

//variant with black hands used by assault
actor assaultPistol : usPistol
{
	Decal "BulletChip"
	States
	{
		Ready:
			BPIS A 1 A_WeaponReady
			Loop
		Deselect:
			BPIS A 0 A_Lower
			BPIS A 1 A_Lower
			Loop
		Select:
			BPIS A 0 A_Raise
			BPIS A 1 A_Raise
			Loop
		Fire:
			BPIS A 0 A_FireBullets(2.4,1.514,-1,6) //why does zandro not have spawnheight yet? absolute rubbish source port smh
			BPIS A 0 A_PlaySound ("weapons/pistolx", CHAN_WEAPON)
			BPIS A 0 A_PlaySound ("weapons/pistoldist", CHAN_5)
			BPIS A 0 A_FireCustomMissile("9MilCaseSpawner",0,0)
			BPIS B 1 Bright A_Light(4) //god i don't miss the ancient method of doing this, need this to be 8 frames length in total about 4.375 shots per second
			BPIS C 1 A_Light(2)
			BPIS DE 1
			BPIS F 1 A_JumpIf(CallACS("checkpistolreload"),"Reload")
			BPIS GH 1 
			Goto Ready
		Reload:
			BPIR A 0 A_JumpIfNoAmmo("Ready")
			BPIR ABCD 2
			BPIR E 2 A_PlaySound("weapons/pistol/magout", CHAN_7)
			BPIR FGH 2
			BPIR I 2 A_PlaySound("weapons/pistol/magdrop",CHAN_6)
			BPIR JKLM 2
			BPIR N 2 A_PlaySound("weapons/pistol/magslide",CHAN_7)
			BPIR OPQ 2
			BPIR R 2 A_PlaySound("weapons/pistol/magin",CHAN_7)
			BPIR ST 2
			BPIR U 2 A_PlaySound("weapons/pistol/slide",CHAN_7)
			BPIR VWXYZ 2
			BPIS IJ 2
			goto Ready
		Spawn:
			PIST A -1
			Stop
	}
}

actor 9MilCase : CaseBase
{
	scale 0.25
	bouncesound "weapons/pistol/casebounce"
	states
	{
		spawn:
			CAS3 A 0 NoDelay ACS_NamedExecuteWithResult("attachcasingsteamer")
			CAS3 A 3 A_Jump(256,"xloop","yloop","zloop")
		xloop:
			CAS3 ABCDEFGH 3
			loop
		yloop:
			CAS3 ABCDEFGH 2
			loop
		zloop:
			CAS3 ABCDEFGH 1
			loop
		death:
			CAS3 B 1 A_Jump(256,"d1","d2")
		d1:
			CAS3 B 1
			goto bedead
		d2:
			CAS3 F 1
			goto bedead
	}
}

actor 9MilCaseSpawner : CaseSpawnerBase
{
	speed 8
	states
	{
		spawn:
			TNT1 A 0 NoDelay A_Stop
			TNT1 A 0 ACS_NamedExecuteWithResult("inherittargvel")
			TNT1 A 1 A_SpawnItemEx("9MilCase",0,0,0,2*cos(angle-100) + velx *1.1 + frandom(-1,1),2*sin(angle-100) + vely*1.1 + frandom(-1,1),frandom(3,5) + velz,0,SXF_ABSOLUTEVELOCITY)
			stop
	}
}

actor puffplus : bulletpuff replaces bulletpuff //i forgot why this exists 10/10/22
{
	+puffgetsowner
	+puffonactors
	seeSound "weapons/bullethit"
	states
	{
		spawn:
			PUFF A 0 noDelay A_FaceTarget
			PUFF A 0 A_Jump(128,"tryRicochet")
			PUFF A 0 A_CustomMissile("BulletImpactSmoke",0,0,random(-90,90),CMF_AIMDIRECTION,random(0,-90))
			tryRicochet:
			PUFF A 0 A_Jump(224,"super::spawn")
			//PUFF A 0 A_CustomMissile("SpecialPuff",0,0,random(-60,60),CMF_AIMDIRECTION,random(-60,60))
			PUFF A 0 A_CustomMissile("BulletRicochetSmoke",0,0,random(-90,90),CMF_AIMDIRECTION,random(0,-90))
			PUFF A 0 A_PlaySound("world/ricochet")
			goto super::spawn
		xdeath:
			goto null
	}
}
//it's here so the rifle makes decals but it didn't work so forget it 10/25/22
//now brought back for extra fx

// HEAVY WEAPONS

//XL is a l33t supa h4x0r
actor CooldownHeavyWeaponMessage : Powerup
{
	Powerup.duration 70
}

//dummy inventory slot 5 actor so you cant pickup say a chaingun and ATRL at the same time

actor Slot5Filled : Inventory
{
    Inventory.MaxAmount 1
    +INVENTORY.UNTOSSABLE
    -INVENTORY.INVBAR
	
	//just in case some dumbarse spawns inherited actors in and cry about it crashing the game
	States
    {
    	Spawn:
    		TNT1 A 0
    		Stop
    }
}

//Anti-Tank rocket launcher--
//lore goes here
actor ATRL : RocketLauncher
{
	Inventory.forbiddenTo "unseen"
	Weapon.selectionOrder 9000 //this is NOT your last resort
	Weapon.slotNumber 5
	Weapon.ammoType ""
	+Weapon.ammo_optional
	+inventory.undroppable
	states
	{
		Spawn:
			LAUN A 1
			loop
		Ready:
			ATRL A 1 A_WeaponReady
			loop
		fire:
 			TNT1 A 0 A_Recoil(16)
			ATRL B 2 A_Light(2)
			ATRL C 3 A_Light(3)
			ATRL D 4 A_FireCustomMissile("HEATRocket",0,0)
			ATRL E 3 A_Light(2)
			ATRL F 2 A_Light(0)
			goto Discard
		select:
			ATRL A 1 A_Raise
			loop
		deselect:
			ATRL A 1 A_Lower
			wait
		Discard:
			ATRL A 0 A_FireCustomMissile("DroppedATRLSpawner",0,0)
			ATRL A 0 A_TakeInventory("Slot5Filled", 1)
			ATRL A 5 A_TakeInventory("ATRL",1)
			goto deselect
	}
}

//prevent respawning
actor ATRLSpawner replaces RocketLauncher
{
	//$Sprite LAUNA0
	//$Title ATRL
	+noInteraction
	+noBlockmap
	+noSector
	states
	{
		spawn:
			TNT1 A 0 NoDelay A_SpawnItemEx("ATRLItem")
			stop
	}
}

//dirty hack because non-custominventory items dont support pickup states
actor ATRLItem : CustomInventory
{
	Inventory.forbiddenTo "unseen"
	Inventory.pickupSound "pickup/heavyweapon"
	States
	{
		Spawn:
			LAUN A 1
			Loop
		Pickup:
			TNT1 A 0 A_JumpIfInventory("Slot5Filled",1,"HaveHeavyWeapon")
			ATRL A 0 A_JumpIfInventory("Slasher",1,"lmao")
			TNT1 A 1
			ATRL A 0 A_GiveInventory("Slot5Filled", 1)
			ATRL A 0 A_GiveInventory("ATRL", 1)
			stop
		lmao:
			ATRL A 0
			fail
		HaveHeavyWeapon:
			TNT1 A 0 A_JumpIfInventory("CooldownHeavyWeaponMessage", 1, 3)
			ATRL A 0 A_Log("You cannot carry another heavy weapon")
			TNT1 A 0 A_GiveInventory("CooldownHeavyWeaponMessage", 1)
			TNT1 A 0
			fail
			
	}
}

actor DroppedATRLSpawner : CaseSpawnerBase
{
	speed 6
	-nogravity
	radius 16
	height 8
	states
	{
		spawn:
		TNT1 A 0 NoDelay A_Stop
		TNT1 A 0 ACS_NamedExecuteWithResult("inherittargvel")
		TNT1 A 1 A_SpawnItemEx("DroppedATRL",0,0,0,6*cos(angle) + velx *1.1 + frandom(-0.5,0.5),6*sin(angle) + vely*1.1 + frandom(-1,1),velz,0,SXF_ABSOLUTEVELOCITY)
		stop
	}
}

actor DroppedATRL : CaseBase
{
	radius 16
	height 8
	bounceSound "weapons/atrldrop"
	bounceFactor "0.4"
	states
	{
		spawn:
			LAUN B -1 nodelay ACS_NamedExecuteWithResult("attachcasingsteamer")
			wait
	}
}

//it portrays you as incompotent when you kill yourself with it
//unused
actor usRocket : Rocket replaces Rocket
{
	selfObituary "$OB_S_ROCKET"
}


//not unused
actor HEATRocket : usRocket
{
	//damage 112 //lmao
	//-nogravity
	//+lowgravity
	//explosionRadius 256
	//scale 1.5
	+extremedeath
	radius 4
	height 8
	speed 30
	damage 50
	ProjectileKickback 1000
    damagetype Fire
	deathSound "weapons/ATRLExp"
	states
	{
		spawn:
			MISL A 0 NoDelay A_CustomMissile("GrenadierSteam",0,0,0,CMF_AIMDIRECTION,pitch)
			MISL A 0 A_CustomMissile("GrenadierSteam",0,0,180,CMF_AIMDIRECTION,-pitch)
			xloop:
			MISL A 2 A_SpawnItemEx("FlameDieSmoke",0,0,0,frandom(-5,0),frandom(-3,3))
			loop
		death:
			TNT1 A 0 A_SpawnItemEx("ComicallyLargeExploQuakeMaker")
			TNT1 A 0 A_SetScale(2,2)
			MISL B 2 bright A_Explode(200,256,XF_HURTSOURCE,0,64)
			MISL CBCBCB 1 bright
			MISL C 8 bright
			MISL D 6 bright
			//LEXP A 3 bright
			//LEXP BCD 3 bright
			//LEXP EFGHIJ 5 bright
			stop
	}
}

// ASSAULT CANNON
// State of the Art bang, bang
// It costs $400,000 to fire this weapon for 12 seconds

//idk ill let scripted or goose figure out how chonky the rounds are
//7.62mm i think lmao --sm
actor ChaingunAmmo : Ammo
{
	Inventory.Amount 200
	Inventory.MaxAmount 200
	Inventory.Icon "CLIPA0"
	Decal "BulletChip" //say wha
	States
	{
	Spawn:
		CLIP A -1
		Stop
  }
}

Actor PowerSlowdownHalf : PowerSpeed
{
	Powerup.Duration -2147483647
	Speed 0.5
}

ACTOR AssaultCannon : Chaingun
{
	Weapon.AmmoUse 1
	Weapon.AmmoGive 200
	Weapon.AmmoType "ChaingunAmmo"
	Inventory.forbiddenTo "unseen"
	Weapon.selectionOrder 700
	Weapon.slotNumber 5
	Weapon.Kickback 150
	Inventory.PickupMessage "Picked up an Assault Cannon"
	Obituary "%k reduced the Threat to a steaming pile of flesh."
	Tag "Assault Cannon"
	+Weapon.ammo_optional
	+inventory.undroppable
	Decal "BulletChip"
	States
	{
		Ready:
			CHAG A 1 A_WeaponReady
			Loop
		Deselect:
			TNT1 A 0 A_Light(0)
			TTN1 A 0 A_StopSound(CHAN_WEAPON)
			TNT1 A 0 A_PlaySound("weapons/assaultcannon/stop", CHAN_6)
			CHAG A 0 A_TakeInventory("PowerSlowdownHalf", 1)
			CHAG A 0 A_StopSound(CHAN_6)
			dloop:
			CHAG A 1 A_Lower
			Loop
		Select:
			CHAG A 1 A_Raise
			Loop
		Fire:
			TNT1 A 0 A_PlaySound("weapons/assaultcannon/start", CHAN_6)
			CHAG ABC 3
			CHAG DEF 2
			TNT1 A 0 A_GiveInventory("PowerSlowdownHalf", 1)
			TNT1 A 0 A_PlaySound("weapons/assaultcannon/fire",CHAN_WEAPON,1,1)
			TNT1 A 0 A_PlaySound("weapons/assaultcannon/loop",CHAN_6,1,1)
		FireLoop:
			TNT1 A 0 A_JumpIfInventory("ChaingunAmmo", 1, 1)
			Goto Discard
			CHAF A 1 Bright A_FireBullets(5.6, 3.55, -1, 7)
			TNT1 A 0 A_FireCustomMissile("7MilCaseSpawner",0,0)
			TNT1 A 0 A_Light(5)
			TNT1 A 0 A_PlaySound("weapons/machinegundist",CHAN_5)
			CHAF B 1
			TNT1 A 0 A_JumpIfInventory("ChaingunAmmo", 1, 1)
			Goto Discard
			CHAF C 1 Bright A_FireBullets(5.6, 3.55, -1, 7)
			TNT1 A 0 A_FireCustomMissile("7MilCaseSpawner",0,0)
			TNT1 A 0 A_Light(3)
			TNT1 A 0 A_PlaySound("weapons/machinegundist",CHAN_5)
			CHAF D 1
			TNT1 A 0 A_JumpIfInventory("ChaingunAmmo", 1, 1)
			Goto Discard
			CHAF E 1 Bright A_FireBullets(5.6, 3.55, -1, 7)
			TNT1 A 0 A_FireCustomMissile("7MilCaseSpawner",0,0)
			TNT1 A 0 A_Light(2)
			TNT1 A 0 A_PlaySound("weapons/machinegundist",CHAN_5)
			CHAF F 1
			TNT1 A 0 A_JumpIfInventory("ChaingunAmmo", 1, 1)
			Goto Discard
			CHAF C 1 Bright A_FireBullets(5.6, 3.55, -1, 7)
			TNT1 A 0 A_FireCustomMissile("7MilCaseSpawner",0,0)
			TNT1 A 0 A_Light(3)
			TNT1 A 0 A_PlaySound("weapons/machinegundist",CHAN_5)
			CHAF G 1
			TNT1 A 0 A_ReFire("FireLoop")
			Goto FireStop
		FireStop:
			TNT1 A 0 A_Light(0)
			TTN1 A 0 A_StopSound(CHAN_WEAPON)
			TNT1 A 0 A_PlaySound("weapons/assaultcannon/stop", CHAN_6)
			TNT1 A 0 A_TakeInventory("PowerSlowdownHalf", 1)
			CHAG FED 2
			CHAG CBA 3
			Goto Ready
		Discard:
			TNT1 A 0 A_Light(0)
			TTN1 A 0 A_StopSound(CHAN_WEAPON)
			TNT1 A 0 A_PlaySound("weapons/assaultcannon/stop", CHAN_6)
			TNT1 A 0 A_TakeInventory("PowerSlowdownHalf", 1)
			CHAG FED 2
			CHAG CBA 3
			TNT1 A 0 A_FireCustomMissile("DroppedAssaultCannonSpawner",0,0)
			TNT1 A 0 A_TakeInventory("Slot5Filled", 1)
			TNT1 A 5 A_TakeInventory("AssaultCannon",1)
			goto deselect
		Spawn:
			CGUN A -1
			Stop
	}
}

//prevent respawning
actor ChaingunSpawner replaces Chaingun
{
	//$Sprite CGUNA0
	//$Title Assault Cannon
	+noInteraction
	+noBlockmap
	+noSector
	states
	{
		spawn:
			TNT1 A 0 NoDelay A_SpawnItemEx("AssaultCannonItem")
			stop
	}
}

//dirty hack because non-custominventory items dont support pickup states
actor AssaultCannonItem : CustomInventory
{
	Inventory.forbiddenTo "unseen"
	Inventory.pickupSound "pickup/heavyweapon"
	States
	{
		Spawn:
			CGUN A 1
			Loop
		Pickup:
			TNT1 A 0 A_JumpIfInventory("Slot5Filled",1,"HaveHeavyWeapon")
			ATRL A 0 A_JumpIfInventory("Slasher",1,"lmao")
			TNT1 A 1
			ATRL A 0 A_GiveInventory("Slot5Filled", 1)
			ATRL A 0 A_GiveInventory("AssaultCannon", 1)
			stop
		lmao:
			ATRL A 0
			fail
		HaveHeavyWeapon:
			TNT1 A 0 A_JumpIfInventory("CooldownHeavyWeaponMessage", 1, 3)
			ATRL A 0 A_Log("You cannot carry another heavy weapon")
			TNT1 A 0 A_GiveInventory("CooldownHeavyWeaponMessage", 1)
			TNT1 A 0
			fail
			
	}
}

actor DroppedAssaultCannonSpawner : CaseSpawnerBase
{
	speed 6
	-nogravity
	radius 16
	height 8
	states
	{
		spawn:
		TNT1 A 0 NoDelay A_Stop
		TNT1 A 0 ACS_NamedExecuteWithResult("inherittargvel")
		TNT1 A 1 A_SpawnItemEx("DroppedAssaultCannon",0,0,0,6*cos(angle) + velx *1.1 + frandom(-0.5,0.5),6*sin(angle) + vely*1.1 + frandom(-1,1),velz,0,SXF_ABSOLUTEVELOCITY)
		stop
	}
}

actor DroppedAssaultCannon : CaseBase
{
	radius 16
	height 8
	bounceSound "weapons/atrldrop"
	bounceFactor "0.4"
	states
	{
		spawn:
			CGUN A -1 nodelay ACS_NamedExecuteWithResult("attachcasingsteamer")
			wait
	}
}

//smokes
actor FlameDieSteam : RocketSmokeTrail
{
	renderStyle "add"
	alpha 0.6
	vSpeed 0
	speed 1
	+clientsideonly
	+noblockmap
	states
	{
		spawn:
			TNT1 A 0 NoDelay A_ChangeVelocity(0,0,frandom(0,2))
			TNT1 A 0 A_SetScale(scalex-random(0,1)*scalex*2,scaley)
			RSMK A 5 A_SetTics(random(2,5))
			goto super::spawn+1
	}
}

actor SmallSteam : FlameDieSteam //fp only
{
	scale 0.5
	speed 2
	states
	{
		spawn:
			TNT1 A 0 NoDelay A_ChangeVelocity(0,0,frandom(0,2))
			TNT1 A 0 ACS_NamedExecuteWithResult("inherittargvel")
			//RSMK A 5 A_SetTics(random(2,5))
			goto super::spawn+1
	}
}

actor CaseSteam : FlameDieSteam
{
	scale 0.25
	alpha 0.2
	radius 2
	height 4
	vspeed 0.25
}

actor FlameDieSmoke : FlameDieSteam //i hate inheritance with a passion
{
	renderStyle "translucent"
	alpha 0.2
}


actor FlameStartSmoke : FlameDieSmoke //fp variant
{
	states
	{
		spawn:
			TNT1 A 0 NoDelay A_ChangeVelocity(0,0,frandom(0,2))
			TNT1 A 0 ACS_NamedExecuteWithResult("inherittargvel")
			//
			RSMK A 5 A_SetTics(random(2,5))
			goto super::spawn+1
	}
}

actor BulletImpactSmoke : FlameDieSmoke
{
	+nointeraction
	-clientsideonly
	scale 1
	alpha 0
	speed 2
	states
	{
		spawn:
			RSMK A 0 NoDelay A_SetScale(scalex-random(0,1)*scalex*2,scaley)
			TNT1 A 0 A_ChangeVelocity(0,0,frandom(0,3))
			RSMK AAAAAA 1 A_GiveInventory("funcSmokeStart",1)
			RSMK BBBBBBCCCCCCDDDDDDEEEEEE 1 A_GiveInventory("funcSmokeGrow",1)
			stop
	}
}
actor BulletRicochetSmoke : BulletImpactSmoke
{
	speed 4
}

actor funcSmokeStart : CustomInventory
{
	+Inventory.alwaysPickup
	states
	{
		pickup:
			TNT1 A 0 A_SetScale(scalex+(scalex/abs(scalex))*0.025,scaley+0.025)
			TNT1 A 0 A_ChangeVelocity(0,0,-0.125)
			TNT1 A 0 A_FadeIn(0.05)
			stop
	}
}

actor funcSmokeGrow : CustomInventory
{
	+Inventory.alwaysPickup
	states
	{
		pickup:
			TNT1 A 0 A_SetScale(scalex+(scalex/abs(scalex))*0.025,scaley+0.025)
			TNT1 A 0 A_ChangeVelocity(0,0,-0.125)
			TNT1 A 0 A_FadeOut(0.015+frandom(-0.05,0.05))
			stop
	}
}

actor GrenadierSteam : SmallSteam
{
	speed 6
	states
	{
		spawn:
			RSMK A 0 NoDelay A_SetScale(scalex-random(0,1)*scalex*2,scaley)
			RSMK A 0 A_ChangeVelocity(0,0,frandom(0,2))
			RSMK ABCDE 3
			stop
	}
}

actor FlamerVomitSmoke : GrenadierSteam
{
	renderStyle "translucent"
	alpha 0.3
	speed 8
}

actor DistantGunMaker : DistantExploMaker
{
	PainSound "weapons/machinegundist"
	states
	{
		spawn:
			TNT1 A 0 NoDelay A_Pain
			BAL1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Warp(AAPTR_TARGET,0,0,0,0,WARPF_NOCHECKPOSITION)
			stop
	}
}

actor BloodSpiller 
{
	radius 2
	height 4
	+clientsideonly
	+noblockmap
	+nosector
	reactionTime 10
	painSound "misc/excessivegore"
	states
	{
		spawn:
			TNT1 A 0 NoDelay A_Pain
			xloop:
			TNT1 A 4 A_Warp(AAPTR_TARGET,0,0,32)
			TNT1 A 0 A_CustomMissile("BloodSpill",0,0,random(0,360),CMF_aimDirection,random(0,90))
			TNT1 A 0 A_CountDown
			loop
	}
}

actor BloodSpill : BloodDrippler //inherits from the unseen's grenade blood
{
	speed 16
	var int user_speed;
	states
	{
		spawn:
			TNT1 A 0 nodelay A_SetUserVar("user_speed",random(8,16))
			TNT1 A 0 A_ChangeVelocity(velx*user_speed/32.,vely*user_speed/32.,velz*user_speed/32.,CVF_REPLACE)
			goto super::spawn
	}
}

actor DistantMachineGunMaker : DistantGunMaker
{
	PainSound "weapons/machinegundist"
}









