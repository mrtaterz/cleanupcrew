//base Unseen player class and things that effect it

//An unknown entity that appears to shimmer in the light.
//They say it came from the moon, and worships King Aamon.
//They feed on the dead to bring life to the undead;
//This knowledge of unlife is given to those who wish to seek it's power;
//But only if you renounce your Self and pledge your fealty to King Aamon.


ACTOR Unseen : PlayerPawn
{
  limitedtoteam 1
  Speed 2
  Health 100
  GibHealth 100 //just for fun
  Player.MaxHealth 300
  Radius 16
  Height 56
  Mass 90
  Gravity 0.833333
  Player.JumpZ 16
  PainChance 255
  Player.AirCapacity 0
  Player.DisplayName "The Threat"
  Player.ColorRange 112, 127 //colorey googles lol
  Player.SoundClass "Unseen"
  Player.StartItem "Slasher"
  Player.StartItem "Grenades"
  Player.StartItem "Nades", 2
  Player.StartItem "EvilArmor"
  Player.StartItem "BeastVision", 100 //the activator / indicator
  Player.StartItem "BeastVisionCounter", 2100 //the timer
  //Player.StartItem "PowerIdleInvis"
  Player.WeaponSlot 1, Slasher
  Player.WeaponSlot 2, Grenades
  RenderStyle "Subtract"
  Alpha 0.03
  player.scoreicon "unsni"
  +shadow
  +USEDAMAGEEVENTSCRIPT
  States
  {
  Spawn:
    SHDT A -1
    Loop
  See:
    SHDT ABCD 4
    Loop
  Missile:
    SHDT EFG 4
    Goto Spawn
  Melee:
    SHDT E 6 BRIGHT
    Goto Missile
  Pain: //TODO: Replace all SHDE frames with uninverted SHDT frames to make it all skin compat
	SHDT H 0 ACS_NamedExecuteAlways("ArmorCheck",0)
	//goto dontyell
	//SHDT H 0 A_Jump(192,"dontyell")
	//Pain.explosive:
	//Pain.bfgbarrelsplash:
	//Pain.proxfire:
	SHDT H 0 A_Pain
	//dontyell:
    SHDT H 4 Bright ACS_NamedExecuteAlways("RevealUnseen",0,8)
    SHDT H 4 Bright A_Quake(1,10,0,1,"none")
    Goto Spawn
  Pain.Poison:
	SHDT h 0 ACS_NamedExecuteAlways("ArmorCheck",0)
	SHDT h 0 //A_Pain
  	//SHDT A 0 A_GiveInventory("PowerPoisonGiver",1)
	//SHDT A 0 A_TakeInventory("PowerCrippleJump",1)//anyhow we take and give here to make sure the duration is refreshed
  	//SHDT A 0 A_GiveInventory("PowerCrippleJump",1)//zandronum doesn't have variable height PowerHighJump
	//SHDT A 0 ACS_NamedExecute("bepoison") //we don't need to start and terminate this on the same frame ////we can't start and terminate this on the same frame
	SHDT h 4 Bright ACS_NamedExecuteAlways("RevealUnseen",0,350)
    SHDT h 4 Bright A_Quake(1,10,0,1,"none")
	Goto Spawn
  Pain.fire:
  Pain.Burns:
  	SHDT H 0 ACS_NamedExecuteAlways("ArmorCheck",0)
	SHDT H 0 A_Pain
	SHDT H 0 ACS_NamedExecuteAlways("RevealUnseen",0,8)
	SHDT H 4 Bright ACS_NamedExecuteAlways("Smoulders",0,175)
    SHDT H 4 Bright A_Quake(1,10,0,1,"none")
	Goto spawn
  Pain.Afterburn: //doesn't reveal
	SHDT H 0 ACS_NamedExecuteAlways("ArmorCheck",0)
	SHDT H 0 A_Pain
	SHDT H 4 A_Quake(1,10,0,1,"none")
	Goto spawn
  Pain.Justice: //unused
  	SHDT H 0 ACS_NamedExecuteAlways("ArmorCheck",0)
	SHDT H 0 //A_Pain
    SHDT A 0 A_TakeInventory("PowerJustice",1)
  	SHDT A 0 A_GiveInventory("PowerJustice",1)
	SHDT H 4 Bright ACS_NamedExecuteAlways("RevealUnseen",0,16)
    SHDT H 4 Bright A_Quake(1,10,0,1,"none")
	Goto Spawn
  Death:
    SHDE I 10
	TNT1 A 0 A_SetTranslucent(1,0)
    SHDE J 10 A_PlayerScream
	TNT1 A 0 A_SpawnItemEx("UnseenGhost", 0, 0, 32, 0, 0, 0, 0, 128)
    SHDE K 10 A_NoBlocking
    SHDE L 10
    SHDE M -1
    Stop
  XDeath:
    SHDE N 5 A_Quake(2,32,0,1,"none")
	TNT1 A 0 A_SetTranslucent(1,0)
    SHDE O 5 A_XScream
	TNT1 A 0 A_SpawnItemEx("UnseenGhost", 0, 0, 32, 0, 0, 0, 0, 128)
    SHDE P 5 A_NoBlocking
    SHDE QR 5
    SHDE S -1
    Stop
  }
}



//unused
Actor UnseenBurner //deals burning damage to the threat as a projectile, spawned by ACS
{
	Damage 0
	+NOINTERACTION
	Radius 8
	Height 16
	+NOGRAVITY
	+NODAMAGETHRUST
	Projectile
	DamageType "Afterburn"
	Obituary "$OB_BURNDAMAGE"
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_Warp(AAPTR_Tracer,0,0,0,0,WARPF_NOCHECKPOSITION)
		Death:
			TNT1 A 0 A_Warp(AAPTR_Tracer,0,0,0,0,WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_Explode(5,4)
			TNT1 A 1
			Stop
	}
}

Actor indicator : inventory // lol generic bool indicator i guess
{ //TODO: move to a saner file
	Inventory.Amount 1
	Inventory.MaxAmount 1
}

Actor paining : indicator // are we AWEUGHing //unused
{}

Actor burning : indicator // are we afire? used by ACS
{}

Actor smoulderpuff : bulletpuff //smoulder is a separate actor because it is
{
	states
	{
		spawn:
			EMBR A 4 Bright NoDelay A_Jump(192,"super::spawn")
			EMBR B 4 A_PlaySound("unseen/burn")
			goto melee
	}
}

Actor powerpoison : powerspeed // poison powerup makes you slower
{
	speed 0.5
	+powerspeed.notrail
	powerup.duration -10 //duration must be 5 seconds longer than the effect actually lasts
	powerup.color "green"
	//powerup.colormap 0,1,0,0,0,0
}

actor powerpoisongiver : powerupgiver
{
	powerup.type "powerpoison"
	powerup.color "green"
	inventory.maxAmount 0
	+inventory.autoActivate
	+inventory.alwaysPickup
}

Actor powercripplejump : powerhighjump //poison powerup makes you jump less //unused
{
	powerup.strength 0.20833325
}


Actor powerjustice : powerpoison // slowerer
{
	speed 0.25
	powerup.color 0,0,0,0
	powerup.duration -1
}

//the unseen, thanks to his cybernetic enhancements, gets better armor from pickups
Actor EvilArmor : BasicArmorPickup //green armor
{
  Inventory.Icon "ARM1A0"
  Armor.SavePercent 33.335 //up from 33%
  Armor.SaveAmount 100
}

Actor AbhorrentArmor : BasicArmorPickup //blue armor
{
  Inventory.Icon "ARM2A0"
  Armor.SavePercent 50 //up from 50%
  Armor.SaveAmount 200
}

Actor VileArmor : BasicArmorPickup //red armor
{
  Inventory.icon "ARM3A0"
  Armor.SavePercent 75
  Armor.SaveAmount 300
}

Actor EvilArmorBonus : BasicArmorBonus //armor bonus
{
	Inventory.Icon "ARM1A0"
	Armor.SavePercent 50 //up from 33%
	Armor.SaveAmount 5
	Armor.MaxSaveAmount 300
}

/*
since we can't do framelogic on the character without some serious shid going
on, and i'd prefer not to get ACS involved every frame, the simplest way
to make the character visible turned out to be just giving them a blursphere
*/
/*
Actor powerpoisonblur : powerinvisibility
{
	powerup.duration -5
	powerup.strength 110
}
*/
//occam's razor applies

/* the above workaround has a consequence- since powerinvisibility resets
your renderstyle whenever it wears off, we have this "fallback"
powerinvisibility which (hopefully) will override your default opacity
without effecting your pain blur as well.
*/
/*
Actor poweridleinvis : powerinvisibility
{
	powerup.duration 0x7fffffff
	powerup.strength 100
	powerup.mode translucent
}
*/
//It didn't work.

Actor poisonrocket : Rocket //testing class that allows me to poison myself
{
	damagetype "poison"
}

Actor incendiaryrocket : Rocket //testing class that allows me to immolate myself
{
	damagetype "burns"
}

actor EvilPistol : Pistol //testing actor
{
  AttackSound "weapons/pistol"
  +WEAPON.CHEATNOTWEAPON
  States
  {
  Fire:
    PISG A 4
    PISG B 6 A_FireBullets(5.6,0,1,5,"EvilPuff")
    PISG C 4
    PISG B 5 A_ReFire
    Goto Ready
  }
}

actor UnseenGhost
{
    Radius 1
    Height 1
    Speed 0
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.7
    +NOCLIP
    States
    {
    Spawn:
    Death:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("unseen/ghost")
        ZFSL ABCDEFG 4 Bright
        Stop
    }
}







