//the explosive weapon for the Unseen, an anti-camping option mostly

//UAC standard-issue fragmentation grenades--
//A small improvement made on the iconic "pineapple with included pin,"
//the UAC Mk. 2 hand grenade is standard issue across all UAC forces.
//Pull the pin, toss through the doorway, watch the enemy scream.

//It is not known exactly where the threat acquired these- it is suspected they 
//were taken from one of the armories stationed on-site.

actor Grenades : Weapon
{
	+Weapon.cheatNotWeapon
	+Weapon.explosive
	tag "$TAG_UGREN"
	Inventory.icon "HERTA0"
	Weapon.ammoType "Nades"
	Weapon.ammoUse 1
	Weapon.bobStyle "alpha"
	Weapon.bobSpeed 2
	+Weapon.alt_uses_both
	+noAlert
    +Inventory.Undroppable
	states
	{
		spawn:
			tnt1 a -1
			stop
		select:
			TNT1 A 0 A_JumpIfInventory("doGrenadeHotkey",1,2)
			TNT1 A 0 A_GiveInventory("hotkeyIgnore",1)
			TNT1 A 0 A_JumpIfInventory("Nades",1,"realSelect")
		sel:
			DHRT M 1 A_Raise
			loop
		realSelect:
			DHRT A 1 A_Raise
			loop
		deselect:
			TNT1 A 0 A_TakeInventory("doGrenadeHotkey",1)
			TNT1 A 0 A_TakeInventory("hotkeyIgnore",1)
			TNT1 A 0 A_JumpIfInventory("Nades",1,"realLower")
		des:
			DHRT M 1 A_Lower
			loop
		realLower:
			DHRT A 1 A_Lower
			loop
		ready:
			DHRT M 0 A_JumpIfInventory("Nades",1,"realReady")
			DHRT M 0 A_JumpIfInventory("doGrenadeHotkey",1,"doHotkey")
			DHRT M 1 A_WeaponReady
			loop
		realReady:
			DHRT A 0 A_JumpIfInventory("doGrenadeHotkey",1,"doHotkey")
			DHRT A 1 A_WeaponReady
			loop
		//BEGIN PRIMARY SEQUENCE
		fire:
			DHRT BC 2
			DHRT D 8
			DHRT E 6 A_PlaySound("Unseen/beat")
			DHRT D 6 A_PlaySound("Unseen/beat")
		hold:
			DHRT D 1 A_GiveInventory("HeartbeatToken",1)
			TNT1 A 0 A_JumpIfInventory("HeartbeatToken",29,"startbeat")
			TNT1 A 0 A_Refire
			goto toss
		startbeat:
			TNT1 A 0 A_PlaySound("Unseen/beat")
		beat:
			DHRT E 1 A_GiveInventory("HeartbeatToken",1)
			TNT1 A 0 A_JumpIfInventory("HeartbeatToken",35,"endbeat")
			TNT1 A 0 A_Refire("beat")
			goto toss
		endbeat:
			TNT1 A 0 A_PlaySound("Unseen/beat")
			TNT1 A 0 A_TakeInventory("HeartbeatToken",35)
			goto hold
		toss:
			DHRT FGH 1
			TNT1 A 0 A_AlertMonsters
			DHRT I 2 A_FireCustomMissile("FlyingNade",0,1,0,8,0,5)
			TNT1 A 0 A_TakeInventory("HeartbeatToken",35)
			DHRT JKLM 2
			TNT1 A 0 A_CheckReload
			DHRT HGFDCB 2
			TNT1 A 0 A_JumpIfInventory("doGrenadeHotkey",1,"hotkeyDeselect")
			goto ready
		//END PRIMARY SEQUENCE
		//BEGIN ALTFIRE SEQUENCE
		altfire:
			DHRT BC 2
			DHRT D 8
			DHRT E 6 A_PlaySound("Unseen/beat")
			DHRT D 6 A_PlaySound("Unseen/beat")
		althold:
			DHRT D 1 A_GiveInventory("HeartbeatToken",1)
			TNT1 A 0 A_JumpIfInventory("HeartbeatToken",29,"altstartbeat")
			TNT1 A 0 A_Refire
			goto tossslow
		altstartbeat:
			TNT1 A 0 A_PlaySound("Unseen/beat")
		altbeat:
			DHRT E 1 A_GiveInventory("HeartbeatToken",1)
			TNT1 A 0 A_JumpIfInventory("HeartbeatToken",35,"altendbeat")
			TNT1 A 0 A_Refire("altbeat")
			goto tossslow
		altendbeat:
			TNT1 A 0 A_PlaySound("Unseen/beat")
			TNT1 A 0 A_TakeInventory("HeartbeatToken",35)
			goto hold
		tossslow:
			DHRT FGH 1
			TNT1 A 0 A_AlertMonsters
			DHRT I 2 A_FireCustomMissile("SlowNade",0,1,0,8,0,5)
			TNT1 A 0 A_TakeInventory("HeartbeatToken",35)
			DHRT JKLM 2
			TNT1 A 0 A_CheckReload
			DHRT HGFDCB 2
			TNT1 A 0 A_JumpIfInventory("doGrenadeHotkey",1,"hotkeyDeselect")
			goto ready
		//END ALTFIRE SEQUENCE
		//BEGIN HOTKEY SEQUENCE
		doHotkey:
			DHRT BC 2
			DHRT D 8
			DHRT E 6 A_PlaySound("Unseen/beat")
			DHRT D 6 A_PlaySound("Unseen/beat")
			HNDG H 0 A_JumpIfInventory("doGrenadeHolding",1,"hotkeyHold")
			goto tossslow
		hotkeyHold:
			DHRT D 1 A_GiveInventory("HeartbeatToken",1)
			TNT1 A 0 A_JumpIfInventory("HeartbeatToken",29,"hotkeystartbeat")
			TNT1 A 0 A_JumpIfInventory("doGrenadeHolding",1,"hotkeyHold")
			goto toss
		hotkeyStartbeat:
			TNT1 A 0 A_PlaySound("Unseen/beat")
		hotkeyBeat:
			DHRT E 1 A_GiveInventory("HeartbeatToken",1)
			TNT1 A 0 A_JumpIfInventory("HeartbeatToken",35,"hotkeyEndbeat")
			TNT1 A 0 A_JumpIfInventory("doGrenadeHolding",1,"hotkeyBeat")
			goto toss
		hotkeyEndbeat:
			TNT1 A 0 A_PlaySound("Unseen/beat")
			TNT1 A 0 A_TakeInventory("HeartbeatToken",35)
			goto hotkeyHold
		hotkeyDeselect:
			TNT1 A 0 A_JumpIfInventory("doGrenadeHolding",1,"doHotkey")
			TNT1 A 0 A_TakeInventory("doGrenadeHotkey",1)
			TNT1 A 0 A_JumpIfInventory("HotkeyIgnore",1,2)
			TNT1 A 0 A_SelectWeapon("Slasher")
			TNT1 A 0
			goto ready
		//END HOTKEY SEQUENCE
	}
}

actor doGrenadeHotkey : Inventory
{
}

actor doGrenadeHolding : Inventory
{
}

actor HotkeyIgnore : Inventory
{
}

//tracks ticks until the next heartbeat.
actor HeartbeatToken : Inventory
{
	Inventory.maxAmount 35
}

actor FlyingNade //the projectile grenade.
{
	radius 8
	height 8
	scale 1
	speed 20
	gravity 0.6
	bounceFactor 0.8
	damage 0
	Projectile
	-nogravity
	-noteleport
	//+usebouncestate
	+randomize
	+dehexplosion
	+canbouncewater
	//bounceType "Doom"
	+bounceonwalls
	+bounceonceilings
	+bounceautooff
	-allowbounceonactors
	seeSound "unseen/toss"
	deathSound "unseen/boom"
	bounceSound "unseen/bounce"
	obituary "$OB_UGREN"
	damageType Grenade
	selfObituary "$OB_S_UGREN"
	decal "BloodSplat"
	States
	{
	Spawn:
		ZGIB AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 2 Bright A_SpawnItemEx("GrenadeBlood")
	Bounce.Floor:
	Die:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_ChangeFlag("Missile",false)
		TNT1 A 0 A_PlaySound("unseen/boom")
	Death:
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_SetScale(0.8,0.8)
		TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx("BloodDrippler",0,0,1,random2(15),random2(15),frandom(0,16))
		TNT1 AAAAAA 0 A_SpawnItemEx("BloodDrippler2",0,0,1,random2(15),random2(15),frandom(0,16))
		HGRN G 8 Bright A_Explode(105,256,XF_HURTSOURCE,true,32)
		HGRN H 6 Bright
		HGRN I 4 Bright
		HGRN J 2 Bright
		Stop
  }
}
actor SlowNade : FlyingNade //the less projectily grenade.
{
	Speed 10
	//-usebouncestate
	bounceType "Doom"
}

actor GrenadeBlood : SpecialBlood
{
	//+clientsideonly
	states
	{
		spawn:
			BLUD C 6
			goto super::spawn+2
	}
}

actor BloodDrippler : GrenadeBlood
{
	-clientsideonly
	states
	{
		spawn:
			BLUD CB 3 NoDelay A_SpawnItemEx("GrenadeBlood")
			loop
	}
}

actor BloodDrippler2 : BloodDrippler
{
	decal "bloodSmear"
}

/*
actor GrenadeBlood : Blood
{
	+dontsplash
	+randomize
	+clientsideonly
	States
	{
		Spawn:
			BL0D ABC 4
			stop
	}
}
*/

actor Nades:Ammo //the ammo for the grenades.
{
	Inventory.maxamount 5
	Inventory.amount 1
	Inventory.icon "HERTA0"
	visibletoplayerclass "unseen" //unseen team
	Inventory.pickupmessage "$GOTNADE"
	Inventory.restrictedto "unseen"
	scale 0.6
	States
	{
		Spawn:
			HERT A -1 Bright
			Loop
	}

}


